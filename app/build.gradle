plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.sergeytsemerov.mockitotests"
        minSdk 23
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    flavorDimensions "default"
    productFlavors {
        fake {
            applicationIdSuffix ".fake"
            versionNameSuffix "-fake"
            buildConfigField "String", "TYPE", '"FAKE"'
        }
        real {
            applicationIdSuffix ".real"
            versionNameSuffix "-real"
            buildConfigField "String", "TYPE", '"REAL"'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

ext {
    retrofitVersion = "2.9.0"
    retrofitConverterGsonVersion = "2.9.0"
    kotlinStdlibVersion = "1.6.10"
    ktxCoreVersion = "1.7.0"
    appcompatVersion = "1.4.1"
    materialVersion = "1.5.0"
    constraintLayoutVersion = "2.1.3"
    junitVersion = "4.13.2"
    junitExtVersion = "1.1.3"
    espressoCoreVersion = "3.4.0"
    espressoIntentsVersion = "3.4.0"
    mockitoCoreVersion = "4.1.0"
    mockitoInlineVersion = "4.1.0"
    mockitoKotlinVersion = "1.5.0"
    robolectricVersion = "4.5.1"
    testCoreVersion = "1.4.0"
    testRunnerVersion = "1.4.0"
    junitTestExtVersion = "1.1.3"
    truthTestExtVersion = "1.4.0"
}

dependencies {

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitConverterGsonVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinStdlibVersion"
    implementation "androidx.core:core-ktx:$ktxCoreVersion"
    implementation "androidx.appcompat:appcompat:$appcompatVersion"
    implementation "com.google.android.material:material:$materialVersion"
    implementation "androidx.constraintlayout:constraintlayout:$constraintLayoutVersion"

    //Test
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$junitExtVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    //Mockito
    testImplementation "org.mockito:mockito-core:$mockitoCoreVersion"
    testImplementation "org.mockito:mockito-inline:$mockitoInlineVersion"
    testImplementation("com.nhaarman:mockito-kotlin:$mockitoKotlinVersion") {
        exclude group: 'org.jetbrains.kotlin'
        exclude group: 'org.mockito'
    }

    //Robolectric
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "androidx.test:core:$testCoreVersion"
    testImplementation "androidx.test:runner:$testRunnerVersion"
    testImplementation "androidx.test.ext:junit:$junitTestExtVersion"
    testImplementation "androidx.test.ext:truth:$truthTestExtVersion"
    testImplementation "androidx.test.espresso:espresso-core:$espressoCoreVersion"
    testImplementation "androidx.test.espresso:espresso-intents:$espressoIntentsVersion"
}